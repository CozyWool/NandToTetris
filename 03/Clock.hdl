/*
* На вход подается 16-битное число period и флаг сброса часов reset.

* У чипа два выхода:
* 1. ticks (0 ≤ ticks < period) — сколько тиков прошло с начала периода,
* 2. loop — равен единице, когда ticks равен нулю.
* 
* Если предыдущее значение ticks оказывается за пределами корректных 
* значений (из-за уменьшения period), нужно вернуть ticks=0, loop=1
*/
CHIP Clock {
    IN period[16], reset;
    OUT ticks[16], loop;

    PARTS:
    Inc16(in=currentTick, out=incTick);
    Not16(in=incTick, out=notIncTick);
    Add16(a=notIncTick, 
          b=period,
          out[15]=incTickGreaterOrEqualPeriod);

    Or(a=incTickGreaterOrEqualPeriod, b=reset, out=shouldReset);
    Mux16(a=incTick, b=false, sel=shouldReset, out=newTick);

    Register(in=newTick, load=true, out=ticks, out=currentTick);
    DFF(in=shouldReset, out=loop);
}