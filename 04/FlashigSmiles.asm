// Таблица соответствия клавиш и позиций смайла:
// Клавиша    Код       left         top       offset
// --------------------------------------------------
// LEFT       130       160          124        3978
// UP         131       256          82         2640  
// RIGHT      132       336          124        3989
// DOWN       133       256          165        5296
// Другая     -         -            -          очистка экрана
// 
// offset = top*32 + left/16

(START)
// Зададим константы
@130
D = A
@left
M = D
@up
MD = D + 1
@right
MD = D + 1
@down
MD = D + 1


@KBD
D = M
// Запомним нажатую клавишу
@input
M = D

// Сравним с клавишой с прошлого шага
@oldInput
D = M - D; 
@ERASE // Очищаем экран, если кнопка изменилась
D; JNE

// Иначе ничего не делаем
@END
0; JMP

// Очистка экрана
(ERASE)
@SCREEN
D = A
@address
M = D // Указатель на начало экрана

// Задаем счётчик
@8192
D = A
@i
M = D 

(LOOP)
    @i
    MD = M - 1 // Уменьшаем счётчик на 1

    @CHECKS
    D;JEQ // Провеяем, нужно ли делать еще одну итерацию, если нет, то переходим к проверкам
    
    @address
    A = M
    M = 0
    
    @address
    M = M + 1

    @LOOP // Возвращаемся в начало цикла
    0;JMP

(CHECKS)
@KBD
D = M
@left
D = D - M // Вычли код left
@delta
M = D // Сохраним KBD - left

// LEFT
@3978
D = A
@offset
M = D // offset для left

@delta
D = M

@END // Если D < left (D < 130), то завершаем
D; JLT

@DRAW
D; JEQ // Если D = left, начать отрисовку с заданным offset

// UP
@2640
D = A
@offset
M = D // offset для up

@delta
MD = M - 1 // Уменьшаем delta на 1, чтобы проверить для up (Например, было D = 131 (up) значит изначльно delta была 131 - 130 = 1. Значит, чтобы проверить для up, нужно вычесть из этой разности еще единицу (131 - 130 - 1) == 0 => up)

// Проверяем для up
@DRAW
D; JEQ 

// RIGHT
@3989
D = A
@offset
M = D // offset для right

@delta
MD = M - 1

// Проверяем для right
@DRAW
D; JEQ 

// DOWN
@5296
D = A
@offset
M = D // offset для down

@delta
MD = M - 1

// Проверяем для down
@DRAW
D; JEQ 

@ERASE // Ни одна кнопка не подошла, завершаем программу без отрисовки
0; JMP

(DRAW)
// Отрисовка смайла
@SCREEN
D = A
@address
M = D // Указатель на начало экрана

@offset
D = M
@address
M = M + D // Смещение в нужную точку

@7224 // 0001110000111000
D = A
@address
A = M
M = D

@32 // Переходим на след. строку смайла
D = A
@address
M = M + D

@7224 // 0001110000111000
D = A
@address
A = M
M = D

@32 // Переходим на след. строку смайла
D = A
@address
M = M + D

@7224 // 0001110000111000
D = A
@address
A = M
M = D

@96 // Переходим на след. строку смайла
D = A
@address
M = M + D

@24582 // 0110000000000110
D = A
@address
A = M
M = D

@32 // Переходим на след. строку смайла
D = A
@address
M = M + D

@14364 // 0011100000011100
D = A
@address
A = M
M = D

@32 // Переходим на след. строку смайла
D = A
@address
M = M + D

@4080 // 0000111111110000
D = A
@address
A = M
M = D
// Смайл отрисован, завершаем программу

(END)
// Запомним клавишу с этого шага
@input
D = M
@oldInput
M = D

// Возвращаемся в начало программы
@START
0;JMP